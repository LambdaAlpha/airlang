# 原子字面量语法

## 单元

`.`

## 比特

`true` 表示真

`false` 表示假

## 整数

`integer(a)` 或 `0a`

整数 `a` 由正负号（`+-`），进制（`BDX`），和数字序列（`0-9a-f`）组成。

`+` 可以省略，进制 `D` 可以省略。

`B` 为二进制，`D` 为十进制，`X` 为十六进制。

数字序列不可为空，数字间可以由下划线（`_`）连接。

十进制正整数 `integer(+Da)`，或 `0+Da` 可以简写为 `a`。

示例：

```air
123
0-123
0+123
1_000_000
0B1
0-B1
0B10
0B1100_0011
0Xa
0-Xa
0Xab_c
integer(123)
integer(-123)
integer(+123)
integer(D123)
integer(B0011)
integer(-Xff)
```

## 数

`number(a)` 或 `0a`

数 `a` 由正负号（`+-`），进制（`BDX`），有效数字（数字序列 `.` 数字序列）和指数（`E` 正负号 数字序列）组成。

`+` 可以省略，进制 `D` 可以省略。

`B` 为二进制，`D` 为十进制，`X` 为十六进制。

数字间可以由下划线（`_`）连接。

在 `0a` 形式中，数以小数点（`.`）和指数前缀（`E`）为标志，数字中若没有小数点，也没有指数前缀，则是整数，否则是数。

十进制正有理数 `number(+Da)`，或 `0+Da` 可以简写为 `a`。

示例：

```air
1.0
1.
1.E2
1E8
0-1.23E-12
0-1_2.3_4E2_3
number(1)
number(-1.0)
number(+1E-3)
number(-Xff.aaE-3)
```

## 字节

`byte(a)`

字节 `a` 由进制（`BX`）和数字序列组成。

`B` 为二进制，后接二进制数字（`01`）序列，每八个为一个单位，单位间可以由下划线（`_`）连接。序列可以为空。

`X` 为十六进制，可省略，后接十六进制数字（`0-9a-f`）序列，每两个为一个单位，单位间可以由下划线（`_`）连接。序列可以为空。

示例：

```air
byte()
byte(B)
byte(B00001111)
byte(B00001111_11110000)
byte(X)
byte(X00_ffff_00)
byte(00_ff)
```

## 符号

常规字符：ASCII 码中所有常规字符，从 ` `（32，含）到 `~`（126，含）。即字母（`a-zA-Z`），数字（`0-9`），标点（`` `~!@#$%^&*()_+-=[]{}\|;:'",.<>/? ``）和空格（` `）。

符号：由常规字符组成的字符序列。

### 转义形式

`\'xxx'`

以 `\` 为前缀，可省略。符号以单引号 `'` 定界。

符号中用 `\` 表示转义

- `\\` 表示 `\`
- `\'` 表示 `'`
- `\_` 表示空格 ` `
- `\` 或换行符 `\n` 后面的连续空白字符会被移除

符号若不包含定界符且不加引号不与其他语法冲突，可不加引号。例如：

- `a` 表示 `a`
- `~` 表示 `~`
- `!!` 表示 `!!`

示例：

```air
a
a_string
str2
%
+
+=
+u
->
'+\''
;a
a;
:a
a:
?a
a?
;
:
!
?
''
'\''
'true'
'\'@'
'\'123'
'"'
'"a'
'1'
'1a'
'+1.0'
\'abc\_def'
```

### 多行原始形式

多行原始形式由以下部分构成

1. `/` 为前缀
2. `'`
3. 原始符号序列，不做任何转义，直到换行
4. 换行后的缩进将被忽略
5. `|`
6. 如果是 `'`，则结束字符串，如果是 ` `，则回到 3。

示例：

```air
/'`~!@#$%^&*()-_=+[{]}\|;:'",<.>/?
|'

/'
| \\\\'
| ''''
|  \
  | \'
|'
```

## 字符串

### 转义形式

`\"xxx"`

以 `\` 为前缀，可省略。字符串前后用 `"` 定界。

字符串支持 unicode 字符集。

字符串中用 `\` 表示转义

- `\_` 表示空格 ` `
- `\t` 表示制表符
- `\n` 表示换行符
- `\r` 表示回车符
- `\\` 表示 `\`
- `\"` 表示 `"`
- `\u(xxx)` 表示 unicode。`xxx` 为 unicode 的编码的十六进制表示。
- `\` 或换行符 `\n` 后面的连续空白字符会被移除

示例：

```air
"hello world"
"hello\n\tworld"
"🜁🜂🜃🜄"
"\u(1F701)"
  "- a
  \  - b
  \  - c
  "
\"abc\ndef"
```

### 多行原始形式

多行原始形式由以下部分构成

1. `/` 为前缀
2. `"`
3. 原始字符序列，不做任何转义，直到换行，换行是否生效要看 5 中的换行指示符
4. 换行后的缩进将被忽略
5. 接一个换行指示符（`-` 换行，`|` 不换行）
6. 如果是 `"`，则结束字符串，如果是 ` `，则回到 3。

示例：

```air
/"`~!@#$%^&*()-_=+[{]}\|;:'",<.>/?
|"

/"
| \\\\"
| """"
-  \
  | \"
-"
```
